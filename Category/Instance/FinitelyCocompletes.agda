{-# OPTIONS --without-K --safe #-}

open import Level using (Level; suc; _âŠ”_)

module Category.Instance.FinitelyCocompletes {o â„“ e : Level} where

import Category.Instance.One.Properties as OneProps

open import Categories.Category using (_[_,_])
open import Categories.Category.BinaryProducts using (BinaryProducts)
open import Categories.Category.Cartesian using (Cartesian)
open import Categories.Category.Core using (Category)
open import Categories.Category.Helper using (categoryHelper)
open import Categories.Category.Instance.Cats using (Cats)
open import Categories.Category.Instance.One using (One; One-âŠ¤)
open import Categories.Category.Monoidal.Instance.Cats using () renaming (module Product to Products)
open import Categories.Category.Product using (Ï€Ë¡; Ï€Ê³; _â€»_; _â‚_) renaming (Product to ProductCat)
open import Categories.Diagram.Coequalizer using (IsCoequalizer)
open import Categories.Functor using (Functor) renaming (id to idF)
open import Categories.NaturalTransformation.NaturalIsomorphism using (_â‰ƒ_; associator; unitorË¡; unitorÊ³; module â‰ƒ; _â“˜â‚•_)
open import Categories.Object.Coproduct using (IsCoproduct)
open import Categories.Object.Initial using (IsInitial)
open import Categories.Object.Product.Core using (Product)
open import Category.Cocomplete.Finitely.Bundle using (FinitelyCocompleteCategory)
open import Category.Cocomplete.Finitely.Product using (FinitelyCocomplete-Ã—)
open import Data.Product.Base using (_,_; projâ‚; projâ‚‚; map; dmap; zipâ€²)
open import Function.Base using (id; flip)
open import Functor.Exact using (âˆ˜-RightExactFunctor; RightExactFunctor; idREF; IsRightExact; rightexact)

FinitelyCocompletes : Category (suc (o âŠ” â„“ âŠ” e)) (o âŠ” â„“ âŠ” e) (o âŠ” â„“ âŠ” e)
FinitelyCocompletes = categoryHelper record
    { Obj = FinitelyCocompleteCategory o â„“ e
    ; _â‡’_ = RightExactFunctor
    ; _â‰ˆ_ = Î» F G â†’ REF.F F â‰ƒ REF.F G
    ; id = idREF
    ; _âˆ˜_ = âˆ˜-RightExactFunctor
    ; assoc = Î» {_ _ _ _ F G H} â†’ associator (REF.F F) (REF.F G) (REF.F H)
    ; identityË¡ = unitorË¡
    ; identityÊ³ = unitorÊ³
    ; equiv = record
        { refl = â‰ƒ.refl
        ; sym = â‰ƒ.sym
        ; trans = â‰ƒ.trans
        }
    ; âˆ˜-resp-â‰ˆ = _â“˜â‚•_
    }
  where
    module REF = RightExactFunctor

One-FCC : FinitelyCocompleteCategory o â„“ e
One-FCC = record
    { U = One
    ; finCo = OneProps.finitelyCocomplete
    }

_Ã—_
    : FinitelyCocompleteCategory o â„“ e
    â†’ FinitelyCocompleteCategory o â„“ e
    â†’ FinitelyCocompleteCategory o â„“ e
_Ã—_ ğ’ ğ’Ÿ = record
    { U = ProductCat ğ’.U ğ’Ÿ.U
    ; finCo = FinitelyCocomplete-Ã— ğ’.finCo ğ’Ÿ.finCo
    }
  where
    module ğ’ = FinitelyCocompleteCategory ğ’
    module ğ’Ÿ = FinitelyCocompleteCategory ğ’Ÿ

module _ (ğ’ ğ’Ÿ : FinitelyCocompleteCategory o â„“ e) where

  private
    module ğ’ = FinitelyCocompleteCategory ğ’
    module ğ’Ÿ = FinitelyCocompleteCategory ğ’Ÿ
    module ğ’Ã—ğ’Ÿ = FinitelyCocompleteCategory (ğ’ Ã— ğ’Ÿ)

  Ï€Ë¡-RightExact : IsRightExact (ğ’ Ã— ğ’Ÿ) ğ’ Ï€Ë¡
  Ï€Ë¡-RightExact = record
      { F-resp-âŠ¥ = F-resp-âŠ¥
      ; F-resp-+ = F-resp-+
      ; F-resp-coeq = F-resp-coeq
      }
    where
      F-resp-âŠ¥
          : {(A , B) : ğ’Ã—ğ’Ÿ.Obj}
          â†’ IsInitial ğ’Ã—ğ’Ÿ.U (A , B)
          â†’ IsInitial ğ’.U A
      F-resp-âŠ¥ {A , B} initial = record
          { ! = Î» { {C} â†’ projâ‚ (! {C , B}) }
          ; !-unique = Î» { f â†’ projâ‚ (!-unique (f , ğ’Ÿ.id)) }
          }
        where
          open IsInitial initial
      F-resp-+
          : {(Câ‚ , Dâ‚) (Câ‚‚ , Dâ‚‚) (Câ‚ƒ , Dâ‚ƒ) : ğ’Ã—ğ’Ÿ.Obj}
            {(iâ‚-c , iâ‚-d) : ğ’Ã—ğ’Ÿ.U [ (Câ‚ , Dâ‚) , (Câ‚ƒ , Dâ‚ƒ) ]}
            {(iâ‚‚-c , iâ‚‚-d) : ğ’Ã—ğ’Ÿ.U [ (Câ‚‚ , Dâ‚‚) , (Câ‚ƒ , Dâ‚ƒ) ]}
          â†’ IsCoproduct (ProductCat ğ’.U ğ’Ÿ.U) (iâ‚-c , iâ‚-d) (iâ‚‚-c , iâ‚‚-d)
          â†’ IsCoproduct ğ’.U iâ‚-c iâ‚‚-c
      F-resp-+ {_} {_} {_} {iâ‚-c , iâ‚-d} {iâ‚‚-c , iâ‚‚-d} isCoproduct = record
          { [_,_] = Î» { hâ‚ hâ‚‚ â†’ projâ‚ (copairing (hâ‚ , iâ‚-d) (hâ‚‚ , iâ‚‚-d)) }
          ; injectâ‚ = projâ‚ injectâ‚
          ; injectâ‚‚ = projâ‚ injectâ‚‚
          ; unique = Î» { eqâ‚ eqâ‚‚ â†’ projâ‚ (unique (eqâ‚ , ğ’Ÿ.identityË¡) (eqâ‚‚ , ğ’Ÿ.identityË¡)) }
          }
        where
          open IsCoproduct isCoproduct renaming ([_,_] to copairing)
      F-resp-coeq
          : {(Câ‚ , Dâ‚) (Câ‚‚ , Dâ‚‚) (Câ‚ƒ , Dâ‚ƒ) : ğ’Ã—ğ’Ÿ.Obj}
            {f g : ğ’Ã—ğ’Ÿ.U [ (Câ‚ , Dâ‚) , (Câ‚‚ , Dâ‚‚) ]}
            {h : ğ’Ã—ğ’Ÿ.U [ (Câ‚‚ , Dâ‚‚) , (Câ‚ƒ , Dâ‚ƒ) ]}
          â†’ IsCoequalizer (ProductCat ğ’.U ğ’Ÿ.U) f g h
          â†’ IsCoequalizer ğ’.U (projâ‚ f) (projâ‚ g) (projâ‚ h)
      F-resp-coeq isCoequalizer = record
          { equality = projâ‚ equality
          ; coequalize = Î» { eq â†’ projâ‚ (coequalize (eq , projâ‚‚ equality)) }
          ; universal = projâ‚ universal
          ; unique = Î» { eq â†’ projâ‚Â (unique (eq , ğ’Ÿ.Equiv.sym ğ’Ÿ.identityË¡)) }
          }
        where
          open IsCoequalizer isCoequalizer

  Ï€Ê³-RightExact : IsRightExact (ğ’ Ã— ğ’Ÿ) ğ’Ÿ Ï€Ê³
  Ï€Ê³-RightExact = record
      { F-resp-âŠ¥ = F-resp-âŠ¥
      ; F-resp-+ = F-resp-+
      ; F-resp-coeq = F-resp-coeq
      }
    where
      F-resp-âŠ¥
          : {(A , B) : ğ’Ã—ğ’Ÿ.Obj}
          â†’ IsInitial ğ’Ã—ğ’Ÿ.U (A , B)
          â†’ IsInitial ğ’Ÿ.U B
      F-resp-âŠ¥ {A , B} initial = record
          { ! = Î» { {C} â†’ projâ‚‚ (! {A , C}) }
          ; !-unique = Î» { f â†’ projâ‚‚ (!-unique (ğ’.id , f)) }
          }
        where
          open IsInitial initial
      F-resp-+
          : {(Câ‚ , Dâ‚) (Câ‚‚ , Dâ‚‚) (Câ‚ƒ , Dâ‚ƒ) : ğ’Ã—ğ’Ÿ.Obj}
            {(iâ‚-c , iâ‚-d) : ğ’Ã—ğ’Ÿ.U [ (Câ‚ , Dâ‚) , (Câ‚ƒ , Dâ‚ƒ) ]}
            {(iâ‚‚-c , iâ‚‚-d) : ğ’Ã—ğ’Ÿ.U [ (Câ‚‚ , Dâ‚‚) , (Câ‚ƒ , Dâ‚ƒ) ]}
          â†’ IsCoproduct ğ’Ã—ğ’Ÿ.U (iâ‚-c , iâ‚-d) (iâ‚‚-c , iâ‚‚-d)
          â†’ IsCoproduct ğ’Ÿ.U iâ‚-d iâ‚‚-d
      F-resp-+ {_} {_} {_} {iâ‚-c , iâ‚-d} {iâ‚‚-c , iâ‚‚-d} isCoproduct = record
          { [_,_] = Î» { hâ‚ hâ‚‚ â†’ projâ‚‚ (copairing (iâ‚-c , hâ‚) (iâ‚‚-c , hâ‚‚)) }
          ; injectâ‚ = projâ‚‚ injectâ‚
          ; injectâ‚‚ = projâ‚‚ injectâ‚‚
          ; unique = Î» { eqâ‚ eqâ‚‚ â†’ projâ‚‚ (unique (ğ’.identityË¡ , eqâ‚) (ğ’.identityË¡ , eqâ‚‚)) }
          }
        where
          open IsCoproduct isCoproduct renaming ([_,_] to copairing)
      F-resp-coeq
          : {(Câ‚ , Dâ‚) (Câ‚‚ , Dâ‚‚) (Câ‚ƒ , Dâ‚ƒ) : ğ’Ã—ğ’Ÿ.Obj}
            {f g : ğ’Ã—ğ’Ÿ.U [ (Câ‚ , Dâ‚) , (Câ‚‚ , Dâ‚‚) ]}
            {h : ğ’Ã—ğ’Ÿ.U [ (Câ‚‚ , Dâ‚‚) , (Câ‚ƒ , Dâ‚ƒ) ]}
          â†’ IsCoequalizer ğ’Ã—ğ’Ÿ.U f g h
          â†’ IsCoequalizer ğ’Ÿ.U (projâ‚‚ f) (projâ‚‚ g) (projâ‚‚ h)
      F-resp-coeq isCoequalizer = record
          { equality = projâ‚‚ equality
          ; coequalize = Î» { eq â†’ projâ‚‚ (coequalize (projâ‚ equality , eq)) }
          ; universal = projâ‚‚ universal
          ; unique = Î» { eq â†’ projâ‚‚Â (unique (ğ’.Equiv.sym ğ’.identityË¡ , eq)) }
          }
        where
          open IsCoequalizer isCoequalizer

module _ where

  open FinitelyCocompleteCategory using (U)

  IsRightExact-â€»
      : {ğ’ ğ’Ÿ â„° : FinitelyCocompleteCategory o â„“ e}
        (F : Functor (U ğ’) (U ğ’Ÿ))
        (G : Functor (U ğ’) (U â„°))
      â†’ IsRightExact ğ’ ğ’Ÿ F
      â†’ IsRightExact ğ’ â„° G
      â†’ IsRightExact ğ’ (ğ’Ÿ Ã— â„°) (F â€» G)
  IsRightExact-â€» {ğ’} {ğ’Ÿ} {â„°} F G isRightExact-F isRightExact-G = recordÂ 
      { F-resp-âŠ¥ = F-resp-âŠ¥â€²
      ; F-resp-+ = F-resp-+â€²
      ; F-resp-coeq = F-resp-coeqâ€²
      }
    where
      module ğ’ = FinitelyCocompleteCategory ğ’
      module ğ’Ÿ = FinitelyCocompleteCategory ğ’Ÿ
      module â„° = FinitelyCocompleteCategory â„°
      open IsRightExact isRightExact-F
      open IsRightExact isRightExact-G renaming (F-resp-âŠ¥ to G-resp-âŠ¥; F-resp-+ to G-resp-+; F-resp-coeq to G-resp-coeq)
      module F = Functor F
      module G = Functor G
      F-resp-âŠ¥â€²
          : {A : ğ’.Obj}
          â†’ IsInitial ğ’.U A
          â†’ IsInitial (ProductCat ğ’Ÿ.U â„°.U) (F.â‚€ A , G.â‚€ A)
      F-resp-âŠ¥â€² A-isInitial = record
          { ! = F[A]-isInitial.! , G[A]-isInitial.!
          ; !-unique = dmap F[A]-isInitial.!-unique G[A]-isInitial.!-unique
          }
        where
          module F[A]-isInitial = IsInitial (F-resp-âŠ¥ A-isInitial)
          module G[A]-isInitial = IsInitial (G-resp-âŠ¥ A-isInitial)
      F-resp-+â€²
          : {A B C : ğ’.Obj} {iâ‚ : ğ’.U [ A , C ]} {iâ‚‚ : ğ’.U [ B , C ]}
          â†’ IsCoproduct ğ’.U iâ‚ iâ‚‚
          â†’ IsCoproduct (ProductCat ğ’Ÿ.U â„°.U) (F.â‚ iâ‚ , G.â‚ iâ‚) (F.â‚ iâ‚‚ , G.â‚ iâ‚‚)
      F-resp-+â€² {A} {B} {A+B} A+B-isCoproduct = record
          { [_,_] = zipâ€² F[A+B]-isCoproduct.[_,_] G[A+B]-isCoproduct.[_,_]
          ; injectâ‚ = F[A+B]-isCoproduct.injectâ‚ , G[A+B]-isCoproduct.injectâ‚
          ; injectâ‚‚ = F[A+B]-isCoproduct.injectâ‚‚ , G[A+B]-isCoproduct.injectâ‚‚
          ; unique = zipâ€² F[A+B]-isCoproduct.unique G[A+B]-isCoproduct.unique
          }
        where
          module F[A+B]-isCoproduct = IsCoproduct (F-resp-+ A+B-isCoproduct)
          module G[A+B]-isCoproduct = IsCoproduct (G-resp-+ A+B-isCoproduct)
      F-resp-coeqâ€²
          : {A B C : ğ’.Obj} {f g : ğ’.U [ A , BÂ ]} {h : ğ’.U [ B , C ]}
          â†’ IsCoequalizer ğ’.U f g h
          â†’ IsCoequalizer (ProductCat ğ’Ÿ.U â„°.U) (F.â‚ f , G.â‚ f) (F.â‚ g , G.â‚ g) (F.â‚ h , G.â‚ h)
      F-resp-coeqâ€² h-isCoequalizer = record
          { equality = F[h]-isCoequalizer.equality , G[h]-isCoequalizer.equality
          ; coequalize = map F[h]-isCoequalizer.coequalize G[h]-isCoequalizer.coequalize
          ; universal = F[h]-isCoequalizer.universal , G[h]-isCoequalizer.universal
          ; unique = map F[h]-isCoequalizer.unique G[h]-isCoequalizer.unique
          }
        where
          module F[h]-isCoequalizer = IsCoequalizer (F-resp-coeq h-isCoequalizer)
          module G[h]-isCoequalizer = IsCoequalizer (G-resp-coeq h-isCoequalizer)

  IsRightExact-â‚
      : {ğ’œ â„¬ ğ’ ğ’Ÿ : FinitelyCocompleteCategory o â„“ e}
        (F : Functor (U ğ’œ) (U ğ’))
        (G : Functor (U â„¬) (U ğ’Ÿ))
      â†’ IsRightExact ğ’œ ğ’ F
      â†’ IsRightExact â„¬ ğ’Ÿ G
      â†’ IsRightExact (ğ’œ Ã— â„¬) (ğ’ Ã— ğ’Ÿ) (F â‚ G)
  IsRightExact-â‚ {ğ’œ} {â„¬} {ğ’} {ğ’Ÿ} F G isRightExact-F isRightExact-G = recordÂ 
      { F-resp-âŠ¥ = F-resp-âŠ¥â€²
      ; F-resp-+ = F-resp-+â€²
      ; F-resp-coeq = F-resp-coeqâ€²
      }
    where
      module ğ’œ = FinitelyCocompleteCategory ğ’œ
      module â„¬ = FinitelyCocompleteCategory â„¬
      module ğ’ = FinitelyCocompleteCategory ğ’
      module ğ’Ÿ = FinitelyCocompleteCategory ğ’Ÿ
      module ğ’œÃ—â„¬ = FinitelyCocompleteCategory (ğ’œ Ã— â„¬)
      module ğ’Ã—ğ’Ÿ = FinitelyCocompleteCategory (ğ’ Ã— ğ’Ÿ)
      open IsRightExact isRightExact-F
      open IsRightExact isRightExact-G renaming (F-resp-âŠ¥ to G-resp-âŠ¥; F-resp-+ to G-resp-+; F-resp-coeq to G-resp-coeq)
      module F = Functor F
      module G = Functor G
      F-resp-âŠ¥â€²
          : {(A , B) : ğ’œÃ—â„¬.Obj}
          â†’ IsInitial ğ’œÃ—â„¬.U (A , B)
          â†’ IsInitial ğ’Ã—ğ’Ÿ.U (F.â‚€ A , G.â‚€ B)
      F-resp-âŠ¥â€² {A , B} A,B-isInitial = record
          { ! = F[A]-isInitial.! , G[B]-isInitial.!
          ; !-unique = dmap F[A]-isInitial.!-unique G[B]-isInitial.!-unique
          }
        where
          module A,B-isInitial = IsInitial A,B-isInitial
          A-isInitial : IsInitial ğ’œ.U A
          A-isInitial = record
              { ! = Î» { {X} â†’ projâ‚ (A,B-isInitial.! {X , B}) }
              ; !-unique = Î» { f â†’ projâ‚ (A,B-isInitial.!-unique (f , â„¬.id)) }
              }
          B-isInitial : IsInitial â„¬.U B
          B-isInitial = record
              { ! = Î» { {X} â†’ projâ‚‚ (A,B-isInitial.! {A , X}) }
              ; !-unique = Î» { f â†’ projâ‚‚ (A,B-isInitial.!-unique (ğ’œ.id , f)) }
              }
          module F[A]-isInitial = IsInitial (F-resp-âŠ¥ A-isInitial)
          module G[B]-isInitial = IsInitial (G-resp-âŠ¥ B-isInitial)
      F-resp-+â€²
          : {A B C : ğ’œÃ—â„¬.Obj} {(iâ‚ , iâ‚â€²) : ğ’œÃ—â„¬.U [ A , C ]} {(iâ‚‚ , iâ‚‚â€²) : ğ’œÃ—â„¬.U [ B , C ]}
          â†’ IsCoproduct ğ’œÃ—â„¬.U (iâ‚ , iâ‚â€²) (iâ‚‚ , iâ‚‚â€²)
          â†’ IsCoproduct ğ’Ã—ğ’Ÿ.U (F.â‚ iâ‚ , G.â‚ iâ‚â€²) (F.â‚ iâ‚‚ , G.â‚ iâ‚‚â€²)
      F-resp-+â€² {A} {B} {A+B} {iâ‚ , iâ‚â€²} {iâ‚‚ , iâ‚‚â€²} A+B,A+Bâ€²-isCoproduct = record
          { [_,_] = zipâ€² F[A+B]-isCoproduct.[_,_] G[A+Bâ€²]-isCoproduct.[_,_]
          ; injectâ‚ = F[A+B]-isCoproduct.injectâ‚ , G[A+Bâ€²]-isCoproduct.injectâ‚
          ; injectâ‚‚ = F[A+B]-isCoproduct.injectâ‚‚ , G[A+Bâ€²]-isCoproduct.injectâ‚‚
          ; unique = zipâ€² F[A+B]-isCoproduct.unique G[A+Bâ€²]-isCoproduct.unique
          }
        where
          module A+B,A+Bâ€²-isCoproduct = IsCoproduct A+B,A+Bâ€²-isCoproduct
          A+B-isCoproduct : IsCoproduct ğ’œ.U iâ‚ iâ‚‚
          A+B-isCoproduct = record
              { [_,_] = Î» { f g â†’ projâ‚ (A+B,A+Bâ€²-isCoproduct.[ (f , iâ‚â€²) , (g , iâ‚‚â€²) ]) }
              ; injectâ‚ = projâ‚ A+B,A+Bâ€²-isCoproduct.injectâ‚
              ; injectâ‚‚ = projâ‚ A+B,A+Bâ€²-isCoproduct.injectâ‚‚
              ; unique = Î» { â‰ˆf â‰ˆg â†’ projâ‚ (A+B,A+Bâ€²-isCoproduct.unique (â‰ˆf , â„¬.identityË¡) (â‰ˆg , â„¬.identityË¡)) }
              }
          A+Bâ€²-isCoproduct : IsCoproduct â„¬.U iâ‚â€² iâ‚‚â€²
          A+Bâ€²-isCoproduct = record
              { [_,_] = Î» { f g â†’ projâ‚‚ (A+B,A+Bâ€²-isCoproduct.[ (iâ‚ , f) , (iâ‚‚ , g) ]) }
              ; injectâ‚ = projâ‚‚ A+B,A+Bâ€²-isCoproduct.injectâ‚
              ; injectâ‚‚ = projâ‚‚ A+B,A+Bâ€²-isCoproduct.injectâ‚‚
              ; unique = Î» { â‰ˆf â‰ˆg â†’ projâ‚‚ (A+B,A+Bâ€²-isCoproduct.unique (ğ’œ.identityË¡ , â‰ˆf) (ğ’œ.identityË¡ , â‰ˆg)) }
              }
          module F[A+B]-isCoproduct = IsCoproduct (F-resp-+ A+B-isCoproduct)
          module G[A+Bâ€²]-isCoproduct = IsCoproduct (G-resp-+ A+Bâ€²-isCoproduct)
      F-resp-coeqâ€²
          : {A B C : ğ’œÃ—â„¬.Obj} {(f , fâ€²) (g , gâ€²) : ğ’œÃ—â„¬.U [ A , BÂ ]} {(h , hâ€²) : ğ’œÃ—â„¬.U [ B , C ]}
          â†’ IsCoequalizer ğ’œÃ—â„¬.U (f , fâ€²) (g , gâ€²) (h , hâ€²)
          â†’ IsCoequalizer ğ’Ã—ğ’Ÿ.U (F.â‚ f , G.â‚ fâ€²) (F.â‚ g , G.â‚ gâ€²) (F.â‚ h , G.â‚ hâ€²)
      F-resp-coeqâ€² {_} {_} {_} {f , fâ€²} {g , gâ€²} {h , hâ€²} h,hâ€²-isCoequalizer = record
          { equality = F[h]-isCoequalizer.equality , G[hâ€²]-isCoequalizer.equality
          ; coequalize = map F[h]-isCoequalizer.coequalize G[hâ€²]-isCoequalizer.coequalize
          ; universal = F[h]-isCoequalizer.universal , G[hâ€²]-isCoequalizer.universal
          ; unique = map F[h]-isCoequalizer.unique G[hâ€²]-isCoequalizer.unique
          }
        where
          module h,hâ€²-isCoequalizer = IsCoequalizer h,hâ€²-isCoequalizer
          h-isCoequalizer : IsCoequalizer ğ’œ.U f g h
          h-isCoequalizer = record
              { equality = projâ‚ h,hâ€²-isCoequalizer.equality
              ; coequalize = Î» { eq â†’ projâ‚ (h,hâ€²-isCoequalizer.coequalize (eq , projâ‚‚ h,hâ€²-isCoequalizer.equality)) }
              ; universal = projâ‚ h,hâ€²-isCoequalizer.universal
              ; unique = Î» { â‰ˆh â†’ projâ‚ (h,hâ€²-isCoequalizer.unique (â‰ˆh , â„¬.Equiv.sym â„¬.identityË¡)) }
              }
          hâ€²-isCoequalizer : IsCoequalizer â„¬.U fâ€² gâ€² hâ€²
          hâ€²-isCoequalizer = record
              { equality = projâ‚‚ h,hâ€²-isCoequalizer.equality
              ; coequalize = Î» { eqâ€² â†’ projâ‚‚ (h,hâ€²-isCoequalizer.coequalize (projâ‚ h,hâ€²-isCoequalizer.equality , eqâ€²)) }
              ; universal = projâ‚‚ h,hâ€²-isCoequalizer.universal
              ; unique = Î» { â‰ˆhâ€² â†’ projâ‚‚ (h,hâ€²-isCoequalizer.unique (ğ’œ.Equiv.sym ğ’œ.identityË¡ , â‰ˆhâ€²)) }
              }

          module F[h]-isCoequalizer = IsCoequalizer (F-resp-coeq h-isCoequalizer)
          module G[hâ€²]-isCoequalizer = IsCoequalizer (G-resp-coeq hâ€²-isCoequalizer)
_Ã—â‚_
    : {ğ’œ â„¬ ğ’ ğ’Ÿ : FinitelyCocompleteCategory o â„“ e}
    â†’ RightExactFunctor ğ’œ ğ’
    â†’ RightExactFunctor â„¬ ğ’Ÿ
    â†’ RightExactFunctor (ğ’œ Ã— â„¬) (ğ’ Ã— ğ’Ÿ)
F Ã—â‚ G = record
    { F = F.F â‚ G.F
    ; isRightExact = IsRightExact-â‚ F.F G.F F.isRightExact G.isRightExact
    }
  where
    module F = RightExactFunctor F
    module G = RightExactFunctor G

FinitelyCocompletes-Products : {ğ’ ğ’Ÿ : FinitelyCocompleteCategory o â„“ e} â†’ Product FinitelyCocompletes ğ’ ğ’Ÿ
FinitelyCocompletes-Products {ğ’} {ğ’Ÿ} = record
    { AÃ—B = ğ’ Ã— ğ’Ÿ
    ; Ï€â‚ = rightexact Ï€Ë¡ (Ï€Ë¡-RightExact ğ’ ğ’Ÿ)
    ; Ï€â‚‚ = rightexact Ï€Ê³ (Ï€Ê³-RightExact ğ’ ğ’Ÿ)
    ; âŸ¨_,_âŸ© = Î» { (rightexact F isF) (rightexact G isG) â†’ rightexact (F â€» G) (IsRightExact-â€»  F G isF isG) }
    ; projectâ‚ = Î» { {_} {rightexact F _} {rightexact G _} â†’ Cats.projectâ‚ {h = F} {i = G} }
    ; projectâ‚‚ = Î» { {_} {rightexact F _} {rightexact G _} â†’ Cats.projectâ‚‚ {h = F} {i = G} }
    ; unique = Cats.unique
    }
  where
    module ğ’ = FinitelyCocompleteCategory ğ’
    module ğ’Ÿ = FinitelyCocompleteCategory ğ’Ÿ
    module Cats = BinaryProducts Products.Cats-has-all

FinitelyCocompletes-BinaryProducts : BinaryProducts FinitelyCocompletes
FinitelyCocompletes-BinaryProducts = record
    { product = FinitelyCocompletes-Products
    }

FinitelyCocompletes-Cartesian : Cartesian FinitelyCocompletes
FinitelyCocompletes-Cartesian = recordÂ 
    { terminal = record
        { âŠ¤ = One-FCC
        ; âŠ¤-is-terminal = _
        }
    ; products = FinitelyCocompletes-BinaryProducts
    }
