{-# OPTIONS --without-K --safe #-}

open import Categories.Category using (Category)
open import Category.Cocomplete.Bundle using (FinitelyCocompleteCategory)
open import Function using (flip)
open import Level using (_âŠ”_)

module Cospan {o â„“ e} (ğ’ : FinitelyCocompleteCategory o â„“ e) where

open FinitelyCocompleteCategory ğ’

open import Categories.Diagram.Duality U using (Pushoutâ‡’coPullback)
open import Categories.Diagram.Pushout U using (Pushout)
open import Categories.Diagram.Pushout.Properties U using (glue; swap; pushout-resp-â‰ˆ)
open import Categories.Morphism U using (_â‰…_; module â‰…)
open import Categories.Morphism.Duality U using (op-â‰…â‡’â‰…)
open import Categories.Morphism.Reasoning U using
    ( switch-fromtoË¡
    ; glueTrianglesË¡
    ; id-comm
    ; id-comm-sym
    ; pullË¡
    ; pullÊ³
    ; assocÂ²''
    ; assocÂ²'
    )

import Categories.Diagram.Pullback op as Pb using (up-to-iso)


private

  variable
    A B C D X Y Z : Obj
    f g h : A â‡’ B

record Cospan (A B : Obj) : Set (o âŠ” â„“) where

  field
    {N} : Obj
    fâ‚    : A â‡’ N
    fâ‚‚    : B â‡’ N

compose : Cospan A B â†’ Cospan B C â†’ Cospan A C
compose câ‚ câ‚‚ = record { fâ‚ = p.iâ‚ âˆ˜ Câ‚.fâ‚ ; fâ‚‚ = p.iâ‚‚ âˆ˜ Câ‚‚.fâ‚‚ }
  where
    module Câ‚ = Cospan câ‚
    module Câ‚‚ = Cospan câ‚‚
    module p = pushout Câ‚.fâ‚‚ Câ‚‚.fâ‚

identity : Cospan A A
identity = record { fâ‚ = id ; fâ‚‚ = id }

compose-3 : Cospan A B â†’ Cospan B C â†’ Cospan C D â†’ Cospan A D
compose-3 câ‚ câ‚‚ câ‚ƒ = record { fâ‚ = Pâ‚ƒ.iâ‚ âˆ˜ Pâ‚.iâ‚ âˆ˜ Câ‚.fâ‚ ; fâ‚‚ = Pâ‚ƒ.iâ‚‚ âˆ˜ Pâ‚‚.iâ‚‚ âˆ˜ Câ‚ƒ.fâ‚‚ }
  where
    module Câ‚ = Cospan câ‚
    module Câ‚‚ = Cospan câ‚‚
    module Câ‚ƒ = Cospan câ‚ƒ
    module Pâ‚ = pushout Câ‚.fâ‚‚ Câ‚‚.fâ‚
    module Pâ‚‚ = pushout Câ‚‚.fâ‚‚ Câ‚ƒ.fâ‚
    module Pâ‚ƒ = pushout Pâ‚.iâ‚‚ Pâ‚‚.iâ‚

record Same (C Câ€² : Cospan A B) : Set (â„“ âŠ” e) where

  module C = Cospan C
  module Câ€² = Cospan Câ€²

  field
    â‰…N : C.N â‰… Câ€².N

  open _â‰…_ â‰…N public

  field
    fromâˆ˜fâ‚â‰ˆfâ‚â€² : from âˆ˜ C.fâ‚ â‰ˆ Câ€².fâ‚
    fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€² : from âˆ˜ C.fâ‚‚ â‰ˆ Câ€².fâ‚‚

same-refl : {C : Cospan A B} â†’ Same C C
same-refl = record
    { â‰…N = â‰….refl
    ; fromâˆ˜fâ‚â‰ˆfâ‚â€² = identityË¡
    ; fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€² = identityË¡
    }

same-sym : {C Câ€² : Cospan A B} â†’ Same C Câ€² â†’ Same Câ€² C
same-sym Câ‰…Câ€² = record
    { â‰…N = â‰….sym â‰…N
    ; fromâˆ˜fâ‚â‰ˆfâ‚â€² = Equiv.sym (switch-fromtoË¡ â‰…N fromâˆ˜fâ‚â‰ˆfâ‚â€²)
    ; fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€² = Equiv.sym (switch-fromtoË¡ â‰…N fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€²)
    }
  where
    open Same Câ‰…Câ€²

same-trans : {C Câ€² Câ€³ : Cospan A B} â†’ Same C Câ€² â†’ Same Câ€² Câ€³ â†’ Same C Câ€³
same-trans Câ‰ˆCâ€² Câ€²â‰ˆCâ€³ = record
    { â‰…N = â‰….trans Câ‰ˆCâ€².â‰…N Câ€²â‰ˆCâ€³.â‰…N
    ; fromâˆ˜fâ‚â‰ˆfâ‚â€² = glueTrianglesË¡ Câ€²â‰ˆCâ€³.fromâˆ˜fâ‚â‰ˆfâ‚â€² Câ‰ˆCâ€².fromâˆ˜fâ‚â‰ˆfâ‚â€²
    ; fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€² = glueTrianglesË¡ Câ€²â‰ˆCâ€³.fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€² Câ‰ˆCâ€².fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€²
    }
  where
    module Câ‰ˆCâ€² = Same Câ‰ˆCâ€²
    module Câ€²â‰ˆCâ€³ = Same Câ€²â‰ˆCâ€³

glue-iâ‚ : (p : Pushout f g) â†’ Pushout h (Pushout.iâ‚ p) â†’ Pushout (h âˆ˜ f) g
glue-iâ‚ p = glue p

glue-iâ‚‚ : (pâ‚ : Pushout f g) â†’ Pushout (Pushout.iâ‚‚ pâ‚) h â†’ Pushout f (h âˆ˜ g)
glue-iâ‚‚ pâ‚ pâ‚‚ = swap (glue (swap pâ‚) (swap pâ‚‚))

up-to-iso : (p pâ€² : Pushout f g) â†’ Pushout.Q p â‰… Pushout.Q pâ€²
up-to-iso p pâ€² = op-â‰…â‡’â‰… (Pb.up-to-iso (Pushoutâ‡’coPullback p) (Pushoutâ‡’coPullback pâ€²))

id-unique : (p : Pushout f g) â†’ (Pushout.universal p) (Pushout.commute p) â‰ˆ id
id-unique p = Equiv.sym (Pushout.unique p identityË¡ identityË¡)

pushout-f-id : Pushout f id
pushout-f-id {_} {_} {f} = record
    { iâ‚ = id
    ; iâ‚‚ = f
    ; commute = id-comm-sym
    ; universal = Î» {B} {hâ‚} {hâ‚‚} eq â†’ hâ‚
    ; unique = Î» {E} {hâ‚} {hâ‚‚} {eq} {j} jâˆ˜iâ‚â‰ˆhâ‚ jâˆ˜iâ‚‚â‰ˆhâ‚‚ â†’ Equiv.sym identityÊ³ â—‹ jâˆ˜iâ‚â‰ˆhâ‚
    ; universalâˆ˜iâ‚â‰ˆhâ‚ = Î» {E} {hâ‚} {hâ‚‚} {eq} â†’ identityÊ³
    ; universalâˆ˜iâ‚‚â‰ˆhâ‚‚ = Î» {E} {hâ‚} {hâ‚‚} {eq} â†’ eq â—‹ identityÊ³
    }
  where
    open HomReasoning

pushout-id-g : Pushout id g
pushout-id-g {_} {_} {g} = record
    { iâ‚ = g
    ; iâ‚‚ = id
    ; commute = id-comm
    ; universal = Î» {B} {hâ‚} {hâ‚‚} eq â†’ hâ‚‚
    ; unique = Î» {E} {hâ‚} {hâ‚‚} {eq} {j} jâˆ˜iâ‚â‰ˆhâ‚ jâˆ˜iâ‚‚â‰ˆhâ‚‚ â†’ Equiv.sym identityÊ³ â—‹ jâˆ˜iâ‚‚â‰ˆhâ‚‚
    ; universalâˆ˜iâ‚â‰ˆhâ‚ = Î» {E} {hâ‚} {hâ‚‚} {eq} â†’ Equiv.sym eq â—‹ identityÊ³
    ; universalâˆ˜iâ‚‚â‰ˆhâ‚‚ = Î» {E} {hâ‚} {hâ‚‚} {eq} â†’ identityÊ³
    }
  where
    open HomReasoning

extend-iâ‚-iso
    : {f : A â‡’ B}
      {g : A â‡’ C}
      (p : Pushout f g)
      (Bâ‰…D : B â‰… D)
    â†’ Pushout (_â‰…_.from Bâ‰…D âˆ˜ f) g
extend-iâ‚-iso {_} {_} {_} {_} {f} {g} p Bâ‰…D = record
    { iâ‚ = P.iâ‚ âˆ˜ Bâ‰…D.to
    ; iâ‚‚ = P.iâ‚‚
    ; commute = begin
          (P.iâ‚ âˆ˜ Bâ‰…D.to) âˆ˜ Bâ‰…D.from âˆ˜ f  â‰ˆâŸ¨ assocÂ²'' âŸ¨
          P.iâ‚ âˆ˜ (Bâ‰…D.to âˆ˜ Bâ‰…D.from) âˆ˜ f  â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ Bâ‰…D.isoË¡ âŸ©âˆ˜âŸ¨refl âŸ©
          P.iâ‚ âˆ˜ id âˆ˜ f                   â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ identityË¡ âŸ©
          P.iâ‚ âˆ˜ f                        â‰ˆâŸ¨ P.commute âŸ©
          P.iâ‚‚ âˆ˜ g                        âˆ
    ; universal = Î» { eq â†’ P.universal (assoc â—‹ eq) }
    ; unique = Î» {_} {hâ‚} {_} {j} â‰ˆâ‚ â‰ˆâ‚‚ â†’
          let
            â‰ˆâ‚â€² = begin
                j âˆ˜ P.iâ‚                        â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ identityÊ³ âŸ¨
                j âˆ˜ P.iâ‚ âˆ˜ id                   â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âˆ˜âŸ¨ Bâ‰…D.isoË¡ âŸ¨
                j âˆ˜ P.iâ‚ âˆ˜ Bâ‰…D.to âˆ˜ Bâ‰…D.from    â‰ˆâŸ¨ assocÂ²' âŸ¨
                (j âˆ˜ P.iâ‚ âˆ˜ Bâ‰…D.to) âˆ˜ Bâ‰…D.from  â‰ˆâŸ¨ â‰ˆâ‚ âŸ©âˆ˜âŸ¨refl âŸ©
                hâ‚ âˆ˜ Bâ‰…D.from                   âˆ
          in P.unique â‰ˆâ‚â€² â‰ˆâ‚‚
    ; universalâˆ˜iâ‚â‰ˆhâ‚ = Î» {E} {hâ‚} {_} {eq} â†’
        begin
            P.universal (assoc â—‹ eq) âˆ˜ P.iâ‚ âˆ˜ Bâ‰…D.to    â‰ˆâŸ¨ sym-assoc âŸ©
            (P.universal (assoc â—‹ eq) âˆ˜ P.iâ‚) âˆ˜ Bâ‰…D.to  â‰ˆâŸ¨ P.universalâˆ˜iâ‚â‰ˆhâ‚ âŸ©âˆ˜âŸ¨refl âŸ©
            (hâ‚ âˆ˜ Bâ‰…D.from) âˆ˜ Bâ‰…D.to                    â‰ˆâŸ¨ assoc âŸ©
            hâ‚ âˆ˜ Bâ‰…D.from âˆ˜ Bâ‰…D.to                      â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ Bâ‰…D.isoÊ³ âŸ©
            hâ‚ âˆ˜ id                                     â‰ˆâŸ¨ identityÊ³ âŸ©
            hâ‚                                          âˆ
    ; universalâˆ˜iâ‚‚â‰ˆhâ‚‚ = P.universalâˆ˜iâ‚‚â‰ˆhâ‚‚
    }
  where
    module P = Pushout p
    module Bâ‰…D = _â‰…_ Bâ‰…D
    open HomReasoning

extend-iâ‚‚-iso
    : {f : A â‡’ B}
      {g : A â‡’ C}
      (p : Pushout f g)
      (Câ‰…D : C â‰… D)
    â†’ Pushout f (_â‰…_.from Câ‰…D âˆ˜ g)
extend-iâ‚‚-iso {_} {_} {_} {_} {f} {g} p Câ‰…D = swap (extend-iâ‚-iso (swap p) Câ‰…D)

compose-idË¡ : {C : Cospan A B} â†’ Same (compose C identity) C
compose-idË¡ {_} {_} {C} = record
    { â‰…N = â‰…P
    ; fromâˆ˜fâ‚â‰ˆfâ‚â€² = begin
          â‰…P.from âˆ˜ P.iâ‚ âˆ˜ C.fâ‚     â‰ˆâŸ¨ assoc âŸ¨
          (â‰…P.from âˆ˜ P.iâ‚) âˆ˜ C.fâ‚   â‰ˆâŸ¨ P.universalâˆ˜iâ‚â‰ˆhâ‚ âŸ©âˆ˜âŸ¨refl âŸ©
          id âˆ˜ C.fâ‚                 â‰ˆâŸ¨ identityË¡ âŸ©
          C.fâ‚                      âˆ
    ; fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€² = begin
          â‰…P.from âˆ˜ P.iâ‚‚ âˆ˜ id       â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ identityÊ³ âŸ©
          â‰…P.from âˆ˜ P.iâ‚‚            â‰ˆâŸ¨ P.universalâˆ˜iâ‚‚â‰ˆhâ‚‚ âŸ©
          C.fâ‚‚                      âˆ
    }
  where
    open HomReasoning
    module C = Cospan C
    P = pushout C.fâ‚‚ id
    module P = Pushout P
    Pâ€² = pushout-f-id {f = C.fâ‚‚}
    â‰…P = up-to-iso P Pâ€²
    module â‰…P = _â‰…_ â‰…P

compose-idÊ³ : {C : Cospan A B} â†’ Same (compose identity C) C
compose-idÊ³ {_} {_} {C} = record
    { â‰…N = â‰…P
    ; fromâˆ˜fâ‚â‰ˆfâ‚â€² = begin
          â‰…P.from âˆ˜ P.iâ‚ âˆ˜ id       â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ identityÊ³ âŸ©
          â‰…P.from âˆ˜ P.iâ‚            â‰ˆâŸ¨ P.universalâˆ˜iâ‚â‰ˆhâ‚ âŸ©
          C.fâ‚                      âˆ
    ; fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€² = begin
          â‰…P.from âˆ˜ P.iâ‚‚ âˆ˜ C.fâ‚‚     â‰ˆâŸ¨ assoc âŸ¨
          (â‰…P.from âˆ˜ P.iâ‚‚) âˆ˜ C.fâ‚‚   â‰ˆâŸ¨ P.universalâˆ˜iâ‚‚â‰ˆhâ‚‚ âŸ©âˆ˜âŸ¨refl âŸ©
          id âˆ˜ C.fâ‚‚                 â‰ˆâŸ¨ identityË¡ âŸ©
          C.fâ‚‚                      âˆ
    }
  where
    open HomReasoning
    module C = Cospan C
    P = pushout id C.fâ‚
    module P = Pushout P
    Pâ€² = pushout-id-g {g = C.fâ‚}
    â‰…P = up-to-iso P Pâ€²
    module â‰…P = _â‰…_ â‰…P

compose-idÂ² : Same {A} (compose identity identity) identity
compose-idÂ² = compose-idË¡

compose-3-right
    : {câ‚ : Cospan A B}
      {câ‚‚ : Cospan B C}
      {câ‚ƒ : Cospan C D}
    â†’ Same (compose câ‚ (compose câ‚‚ câ‚ƒ)) (compose-3 câ‚ câ‚‚ câ‚ƒ)
compose-3-right {_} {_} {_} {_} {câ‚} {câ‚‚} {câ‚ƒ} = record
    { â‰…N = up-to-iso Pâ‚„â€² Pâ‚„
    ; fromâˆ˜fâ‚â‰ˆfâ‚â€² = sym-assoc â—‹ Pâ‚„â€².universalâˆ˜iâ‚â‰ˆhâ‚ âŸ©âˆ˜âŸ¨refl â—‹ assoc
    ; fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€² = sym-assoc â—‹ Pâ‚„â€².universalâˆ˜iâ‚‚â‰ˆhâ‚‚ âŸ©âˆ˜âŸ¨refl
    }
  where
    open HomReasoning
    module Câ‚ = Cospan câ‚
    module Câ‚‚ = Cospan câ‚‚
    module Câ‚ƒ = Cospan câ‚ƒ
    Pâ‚ = pushout Câ‚.fâ‚‚ Câ‚‚.fâ‚
    Pâ‚‚ = pushout Câ‚‚.fâ‚‚ Câ‚ƒ.fâ‚
    module Pâ‚ = Pushout Pâ‚
    module Pâ‚‚ = Pushout Pâ‚‚
    Pâ‚ƒ = pushout Pâ‚.iâ‚‚ Pâ‚‚.iâ‚
    module Pâ‚ƒ = Pushout Pâ‚ƒ
    Pâ‚„ = glue-iâ‚‚ Pâ‚ Pâ‚ƒ
    module Pâ‚„ = Pushout Pâ‚„
    Pâ‚„â€² = pushout Câ‚.fâ‚‚ (Pâ‚‚.iâ‚ âˆ˜ Câ‚‚.fâ‚)
    module Pâ‚„â€² = Pushout Pâ‚„â€²

compose-3-left
    : {câ‚ : Cospan A B}
      {câ‚‚ : Cospan B C}
      {câ‚ƒ : Cospan C D}
    â†’ Same (compose (compose câ‚ câ‚‚) câ‚ƒ) (compose-3 câ‚ câ‚‚ câ‚ƒ)
compose-3-left {_} {_} {_} {_} {câ‚} {câ‚‚} {câ‚ƒ} = record
    { â‰…N = up-to-iso Pâ‚„â€² Pâ‚„
    ; fromâˆ˜fâ‚â‰ˆfâ‚â€² = sym-assoc â—‹ Pâ‚„â€².universalâˆ˜iâ‚â‰ˆhâ‚ âŸ©âˆ˜âŸ¨refl
    ; fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€² = sym-assoc â—‹ Pâ‚„â€².universalâˆ˜iâ‚‚â‰ˆhâ‚‚ âŸ©âˆ˜âŸ¨refl â—‹ assoc
    }
  where
    open HomReasoning
    module Câ‚ = Cospan câ‚
    module Câ‚‚ = Cospan câ‚‚
    module Câ‚ƒ = Cospan câ‚ƒ
    Pâ‚ = pushout Câ‚.fâ‚‚ Câ‚‚.fâ‚
    Pâ‚‚ = pushout Câ‚‚.fâ‚‚ Câ‚ƒ.fâ‚
    module Pâ‚ = Pushout Pâ‚
    module Pâ‚‚ = Pushout Pâ‚‚
    Pâ‚ƒ = pushout Pâ‚.iâ‚‚ Pâ‚‚.iâ‚
    module Pâ‚ƒ = Pushout Pâ‚ƒ
    Pâ‚„ = glue-iâ‚ Pâ‚‚ Pâ‚ƒ
    module Pâ‚„ = Pushout Pâ‚„
    Pâ‚„â€² = pushout (Pâ‚.iâ‚‚ âˆ˜ Câ‚‚.fâ‚‚) Câ‚ƒ.fâ‚
    module Pâ‚„â€² = Pushout Pâ‚„â€²

compose-assoc
    : {câ‚ : Cospan A B}
      {câ‚‚ : Cospan B C}
      {câ‚ƒ : Cospan C D}
    â†’ Same (compose câ‚ (compose câ‚‚ câ‚ƒ)) (compose (compose câ‚ câ‚‚) câ‚ƒ)
compose-assoc = same-trans compose-3-right (same-sym compose-3-left)

compose-sym-assoc
    : {câ‚ : Cospan A B}
      {câ‚‚ : Cospan B C}
      {câ‚ƒ : Cospan C D}
    â†’ Same (compose (compose câ‚ câ‚‚) câ‚ƒ) (compose câ‚ (compose câ‚‚ câ‚ƒ))
compose-sym-assoc = same-trans compose-3-left (same-sym compose-3-right)

compose-equiv
    : {câ‚‚ câ‚‚â€² : Cospan B C}
      {câ‚ câ‚â€² : Cospan A B}
    â†’ Same câ‚‚ câ‚‚â€²
    â†’ Same câ‚ câ‚â€²
    â†’ Same (compose câ‚ câ‚‚) (compose câ‚â€² câ‚‚â€²)
compose-equiv {_} {_} {_} {câ‚‚} {câ‚‚â€²} {câ‚} {câ‚â€²} â‰ˆCâ‚‚ â‰ˆCâ‚ = record
    { â‰…N = up-to-iso P Pâ€³
    ; fromâˆ˜fâ‚â‰ˆfâ‚â€² = begin
          â‰…P.from âˆ˜ P.iâ‚ âˆ˜ Câ‚.fâ‚      â‰ˆâŸ¨ assoc âŸ¨
          (â‰…P.from âˆ˜ P.iâ‚) âˆ˜ Câ‚.fâ‚    â‰ˆâŸ¨ P.universalâˆ˜iâ‚â‰ˆhâ‚ âŸ©âˆ˜âŸ¨refl âŸ©
          (Pâ€².iâ‚ âˆ˜ â‰ˆCâ‚.from) âˆ˜ Câ‚.fâ‚  â‰ˆâŸ¨ assoc âŸ©
          Pâ€².iâ‚ âˆ˜ â‰ˆCâ‚.from âˆ˜ Câ‚.fâ‚    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ â‰ˆCâ‚.fromâˆ˜fâ‚â‰ˆfâ‚â€² âŸ©
          Pâ€².iâ‚ âˆ˜ Câ‚â€².fâ‚              âˆ
    ; fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€² = begin
          â‰…P.from âˆ˜ P.iâ‚‚ âˆ˜ Câ‚‚.fâ‚‚      â‰ˆâŸ¨ assoc âŸ¨
          (â‰…P.from âˆ˜ P.iâ‚‚) âˆ˜ Câ‚‚.fâ‚‚    â‰ˆâŸ¨ P.universalâˆ˜iâ‚‚â‰ˆhâ‚‚ âŸ©âˆ˜âŸ¨refl âŸ©
          (Pâ€².iâ‚‚ âˆ˜ â‰ˆCâ‚‚.from) âˆ˜ Câ‚‚.fâ‚‚  â‰ˆâŸ¨ assoc âŸ©
          Pâ€².iâ‚‚ âˆ˜ â‰ˆCâ‚‚.from âˆ˜ Câ‚‚.fâ‚‚    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ â‰ˆCâ‚‚.fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€² âŸ©
          Pâ€².iâ‚‚ âˆ˜ Câ‚‚â€².fâ‚‚              âˆ
    }
  where
    module Câ‚ = Cospan câ‚
    module Câ‚â€² = Cospan câ‚â€²
    module Câ‚‚ = Cospan câ‚‚
    module Câ‚‚â€² = Cospan câ‚‚â€²
    P = pushout Câ‚.fâ‚‚ Câ‚‚.fâ‚
    Pâ€² = pushout Câ‚â€².fâ‚‚ Câ‚‚â€².fâ‚
    module â‰ˆCâ‚ = Same â‰ˆCâ‚
    module â‰ˆCâ‚‚ = Same â‰ˆCâ‚‚
    Pâ€²â€² : Pushout (â‰ˆCâ‚.to âˆ˜ Câ‚â€².fâ‚‚) (â‰ˆCâ‚‚.to âˆ˜ Câ‚‚â€².fâ‚)
    Pâ€²â€² = extend-iâ‚‚-iso (extend-iâ‚-iso Pâ€² (â‰….sym â‰ˆCâ‚.â‰…N)) (â‰….sym â‰ˆCâ‚‚.â‰…N)
    Pâ€³ : Pushout Câ‚.fâ‚‚ Câ‚‚.fâ‚
    Pâ€³ =
        pushout-resp-â‰ˆ
            Pâ€²â€²
            (Equiv.sym (switch-fromtoË¡ â‰ˆCâ‚.â‰…N â‰ˆCâ‚.fromâˆ˜fâ‚‚â‰ˆfâ‚‚â€²))
            (Equiv.sym (switch-fromtoË¡ â‰ˆCâ‚‚.â‰…N â‰ˆCâ‚‚.fromâˆ˜fâ‚â‰ˆfâ‚â€²))
    module P = Pushout P
    module Pâ€² = Pushout Pâ€²
    â‰…P : P.Q â‰… Pâ€².Q
    â‰…P = up-to-iso P Pâ€³
    module â‰…P = _â‰…_ â‰…P
    open HomReasoning

Cospans : Category o (o âŠ” â„“) (â„“ âŠ” e)
Cospans = record
    { Obj = Obj
    ; _â‡’_ = Cospan
    ; _â‰ˆ_ = Same
    ; id = identity
    ; _âˆ˜_ = flip compose
    ; assoc = compose-assoc
    ; sym-assoc = compose-sym-assoc
    ; identityË¡ = compose-idË¡
    ; identityÊ³ = compose-idÊ³
    ; identityÂ² = compose-idÂ²
    ; equiv = record
        { refl = same-refl
        ; sym = same-sym
        ; trans = same-trans
        }
    ; âˆ˜-resp-â‰ˆ = compose-equiv
    }
