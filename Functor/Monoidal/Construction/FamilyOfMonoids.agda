{-# OPTIONS --without-K --safe #-}

open import Categories.Category using (Category)
open import Categories.Category.Cocartesian using (Cocartesian)
open import Categories.Category.Cocartesian.Bundle using (CocartesianCategory)
open import Categories.Category.Construction.Monoids using (Monoids)
open import Categories.Category.Monoidal.Bundle using (MonoidalCategory)
open import Categories.Functor using (Functor) renaming (_âˆ˜F_ to _âˆ™_)
open import Level using (Level; _âŠ”_)

-- A functor from a cocartesian category ğ’ to Monoids[S]
-- can be turned into a monoidal functor from ğ’ to S

module Functor.Monoidal.Construction.FamilyOfMonoids
    {o oâ€² â„“ â„“â€² e eâ€² : Level}
    {ğ’ : Category o â„“ e}
    (ğ’-+ : Cocartesian ğ’)
    {S : MonoidalCategory oâ€² â„“â€² eâ€²}
    (let module S = MonoidalCategory S)
    (M : Functor ğ’ (Monoids S.monoidal))
  where

import Categories.Category.Monoidal.Reasoning as âŠ—-Reasoning
import Categories.Category.Monoidal.Utilities as âŠ—-Util
import Categories.Morphism.Reasoning as â‡’-Reasoning
import Categories.Object.Monoid as MonoidObject

open import Categories.Category using (module Definitions)
open import Categories.Category.Cocartesian using (module CocartesianMonoidal)
open import Categories.Category.Product using (_â‚_)
open import Categories.Functor.Monoidal using (MonoidalFunctor; IsMonoidalFunctor)
open import Categories.Functor.Properties using ([_]-resp-square; [_]-resp-âˆ˜)
open import Categories.Morphism using (_â‰…_)
open import Categories.NaturalTransformation using (NaturalTransformation; ntHelper)
open import Data.Product using (_,_)
open import Functor.Forgetful.Instance.Monoid S using (Forget)

private

  G : Functor ğ’ S.U
  G = Forget âˆ™ M

  module ğ’ = CocartesianCategory (record { cocartesian = ğ’-+ })
  module ğ’-M = CocartesianMonoidal ğ’ ğ’-+

  ğ’-MC : MonoidalCategory o â„“ e
  ğ’-MC = record { monoidal = ğ’-M.+-monoidal }

  module +-assoc {n} {m} {o} = _â‰…_ (ğ’.+-assoc {n} {m} {o})
  module +-Î» {n} = _â‰…_ (ğ’-M.âŠ¥+Aâ‰…A {n})
  module +-Ï {n} = _â‰…_ (ğ’-M.A+âŠ¥â‰…A {n})

  module G = Functor G
  module M = Functor M

  open MonoidObject S.monoidal using (Monoid; Monoidâ‡’)
  open Monoid renaming (assoc to Î¼-assoc; identityË¡ toÂ Î¼-identityË¡; identityÊ³ to Î¼-identityÊ³)
  open Monoidâ‡’

  open ğ’ using (-+-; _+_; _+â‚_; iâ‚; iâ‚‚; injectâ‚; injectâ‚‚)

  module _ where

    open Category ğ’
    open â‡’-Reasoning ğ’
    open âŠ—-Reasoning ğ’-M.+-monoidal

    module _ {n m o : Obj} where

      private

        +-Î± : (n + m) + o ğ’.â‡’ n + (m + o)
        +-Î± = +-assoc.to {n} {m} {o}

      +-Î±âˆ˜iâ‚‚ : +-Î± âˆ˜ iâ‚‚ â‰ˆ iâ‚‚ âˆ˜ iâ‚‚
      +-Î±âˆ˜iâ‚‚ = injectâ‚‚

      +-Î±âˆ˜iâ‚âˆ˜iâ‚ : (+-Î± âˆ˜ iâ‚) âˆ˜ iâ‚ â‰ˆ iâ‚
      +-Î±âˆ˜iâ‚âˆ˜iâ‚ = injectâ‚ âŸ©âˆ˜âŸ¨refl â—‹ injectâ‚

      +-Î±âˆ˜iâ‚âˆ˜iâ‚‚ : (+-Î± âˆ˜ iâ‚) âˆ˜ iâ‚‚ â‰ˆ iâ‚‚ âˆ˜ iâ‚
      +-Î±âˆ˜iâ‚âˆ˜iâ‚‚ = injectâ‚ âŸ©âˆ˜âŸ¨refl â—‹ injectâ‚‚

    module _ {n : Obj} where

      +-Ïâˆ˜iâ‚ : +-Ï.from {n} âˆ˜ iâ‚ â‰ˆ id
      +-Ïâˆ˜iâ‚ = injectâ‚

      +-Î»âˆ˜iâ‚‚ : +-Î».from {n} âˆ˜ iâ‚‚ â‰ˆ id
      +-Î»âˆ˜iâ‚‚ = injectâ‚‚

  open S
  open â‡’-Reasoning U
  open âŠ—-Reasoning monoidal
  open âŠ—-Util.Shorthands monoidal

  âŠ² : {A : ğ’.Obj} â†’ G.â‚€ A âŠ—â‚€ G.â‚€ A â‡’ G.â‚€ A
  âŠ² {A} = Î¼ (M.â‚€ A)

  â‡’âŠ² : {A B : ğ’.Obj} (f : A ğ’.â‡’ B) â†’ G.â‚ f âˆ˜ âŠ² â‰ˆ âŠ² âˆ˜ G.â‚ f âŠ—â‚ G.â‚ f
  â‡’âŠ² f = preserves-Î¼ (M.â‚ f)

  Îµ : {A : ğ’.Obj} â†’ unit â‡’ G.â‚€ A
  Îµ {A} = Î· (M.â‚€ A)

  â‡’Îµ : {A B : ğ’.Obj} (f : A ğ’.â‡’ B) â†’ G.â‚ f âˆ˜ Îµ â‰ˆ Îµ
  â‡’Îµ f = preserves-Î· (M.â‚ f)

  âŠ²-âŠ— : {n m : ğ’.Obj} â†’ G.â‚€ n âŠ—â‚€ G.â‚€ m â‡’ G.â‚€ (n + m)
  âŠ²-âŠ— = âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚

  module _ {n nâ€² m mâ€² : ğ’.Obj} (f : n ğ’.â‡’ nâ€²) (g : m ğ’.â‡’ mâ€²) where

    open Definitions S.U using (CommutativeSquare)

    leftâ‚ : CommutativeSquare (G.â‚ iâ‚) (G.â‚ f) (G.â‚ (f +â‚ g)) (G.â‚ iâ‚)
    leftâ‚ = [ G ]-resp-square injectâ‚

    leftâ‚‚ : CommutativeSquare (G.â‚ iâ‚‚) (G.â‚ g) (G.â‚ (f +â‚ g)) (G.â‚ iâ‚‚)
    leftâ‚‚ = [ G ]-resp-square injectâ‚‚

    right : CommutativeSquare âŠ² (G.â‚ (f +â‚ g) âŠ—â‚ G.â‚ (f +â‚ g)) (G.â‚ (f +â‚ g)) âŠ²
    right = â‡’âŠ² (f +â‚ g)

    âŠ²-âŠ—-commute :
        CommutativeSquare
            (âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚)
            (G.â‚ f âŠ—â‚ G.â‚ g)
            (G.â‚ (f +â‚ g))
            (âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚)
    âŠ²-âŠ—-commute = glueâ€² right (parallel leftâ‚ leftâ‚‚)

  âŠ²-âŠ—-homo : NaturalTransformation (âŠ— âˆ™ (G â‚ G)) (G âˆ™Â -+-)
  âŠ²-âŠ—-homo = ntHelper record
      { Î· = Î» (n , m) â†’ âŠ²-âŠ— {n} {m}
      ; commute = Î» (f , g) â†’ Equiv.sym (âŠ²-âŠ—-commute f g)
      }

  âŠ²-âŠ—-Î±
      : {n m o : ğ’.Obj}
      â†’ G.â‚ (+-assoc.to {n} {m} {o})
      âˆ˜ (Î¼ (M.â‚€ ((n + m) + o)) âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚)
      âˆ˜ (Î¼ (M.â‚€ (n + m)) âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âŠ—â‚ id
      â‰ˆ (Î¼ (M.â‚€ (n + m + o)) âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚)
      âˆ˜ id âŠ—â‚ (Î¼ (M.â‚€ (m + o)) âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚)
      âˆ˜ Î±â‡’
  âŠ²-âŠ—-Î± {n} {m} {o} = begin
      G.â‚ +-Î± âˆ˜ (âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âˆ˜ (âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âŠ—â‚ id         â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ pullÊ³ mergeâ‚Ê³ âŸ©
      G.â‚ +-Î± âˆ˜ âŠ² âˆ˜ (G.â‚ iâ‚ âˆ˜ âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âŠ—â‚ G.â‚ iâ‚‚                 â‰ˆâŸ¨ extendÊ³ (â‡’âŠ² +-Î±) âŸ©
      âŠ² âˆ˜ G.â‚ +-Î± âŠ—â‚ G.â‚ +-Î± âˆ˜ (G.â‚ iâ‚ âˆ˜ âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âŠ—â‚ G.â‚ iâ‚‚      â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ âŠ—-distrib-over-âˆ˜ âŸ¨
      âŠ² âˆ˜ (G.â‚ +-Î± âˆ˜ G.â‚ iâ‚ âˆ˜ âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âŠ—â‚ (G.â‚ +-Î± âˆ˜ G.â‚ iâ‚‚)     â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ pullË¡ (Equiv.sym G.homomorphism) âŸ©âŠ—âŸ¨ [ G ]-resp-square +-Î±âˆ˜iâ‚‚ âŸ©
      âŠ² âˆ˜ (G.â‚ (+-Î± ğ’.âˆ˜ iâ‚) âˆ˜ âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚‚)      â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ extendÊ³ (â‡’âŠ² (+-Î± ğ’.âˆ˜ iâ‚)) âŸ©âŠ—âŸ¨refl âŸ©
      âŠ² âˆ˜ (âŠ² âˆ˜ G.â‚ (+-Î± ğ’.âˆ˜ iâ‚) âŠ—â‚ G.â‚ (+-Î± ğ’.âˆ˜ iâ‚) âˆ˜ _) âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚‚) â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ (reflâŸ©âˆ˜âŸ¨ âŠ—-distrib-over-âˆ˜) âŸ©âŠ—âŸ¨refl âŸ¨
      âŠ² âˆ˜ (âŠ² âˆ˜ _ âŠ—â‚ (G.â‚ (+-Î± ğ’.âˆ˜ iâ‚) âˆ˜ G.â‚ iâ‚‚)) âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚‚)         â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ (reflâŸ©âˆ˜âŸ¨ [ G ]-resp-âˆ˜ +-Î±âˆ˜iâ‚âˆ˜iâ‚ âŸ©âŠ—âŸ¨ [ G ]-resp-square +-Î±âˆ˜iâ‚âˆ˜iâ‚‚) âŸ©âŠ—âŸ¨refl âŸ©
      âŠ² âˆ˜ (âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚)) âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚‚)              â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ splitâ‚Ë¡ âŸ©
      âŠ² âˆ˜ âŠ² âŠ—â‚ id âˆ˜ (G.â‚ iâ‚ âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚)) âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚‚)        â‰ˆâŸ¨ extendÊ³ (Î¼-assoc (M.â‚€ (n + (m + o)))) âŸ©
      âŠ² âˆ˜ (id âŠ—â‚ âŠ² âˆ˜ Î±â‡’) âˆ˜ (G.â‚ iâ‚ âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚)) âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚‚) â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ assoc âŸ©
      âŠ² âˆ˜ id âŠ—â‚ âŠ² âˆ˜ Î±â‡’ âˆ˜ (G.â‚ iâ‚ âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚)) âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚‚)   â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âˆ˜âŸ¨ assoc-commute-from âŸ©
      âŠ² âˆ˜ id âŠ—â‚ âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ ((G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚) âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚‚)) âˆ˜ Î±â‡’   â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ pullË¡ mergeâ‚‚Ë¡ âŸ©
      âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ (âŠ² âˆ˜ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚) âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚‚)) âˆ˜ Î±â‡’         â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âŠ—âŸ¨ (reflâŸ©âˆ˜âŸ¨ âŠ—-distrib-over-âˆ˜) âŸ©âˆ˜âŸ¨refl âŸ©
      âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ (âŠ² âˆ˜ G.â‚ iâ‚‚ âŠ—â‚ G.â‚ iâ‚‚ âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âˆ˜ Î±â‡’            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âŠ—âŸ¨ (extendÊ³ (â‡’âŠ² iâ‚‚)) âŸ©âˆ˜âŸ¨refl âŸ¨
      âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âˆ˜ Î±â‡’                      â‰ˆâŸ¨ pushÊ³ (pushË¡ splitâ‚‚Ê³) âŸ©
      (âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âˆ˜ id âŠ—â‚ (âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âˆ˜ Î±â‡’              âˆ
    where
      +-Î± : (n + m) + o ğ’.â‡’ n + (m + o)
      +-Î± = +-assoc.to {n} {m} {o}

  module _ {A B : ğ’.Obj} (f : AÂ ğ’.â‡’ B) where

    âŠ²-ÎµÊ³ : âŠ² âˆ˜ G.â‚ f âŠ—â‚ Îµ â‰ˆ G.â‚ f âˆ˜ Ïâ‡’
    âŠ²-ÎµÊ³ = begin
        âŠ² âˆ˜ G.â‚ f âŠ—â‚ Îµ            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ serializeâ‚‚â‚ âŸ©
        âŠ² âˆ˜ id âŠ—â‚ Îµ âˆ˜ G.â‚ f âŠ—â‚ id â‰ˆâŸ¨ pullË¡ (Equiv.sym (Î¼-identityÊ³Â (M.â‚€ B))) âŸ©
        Ïâ‡’ âˆ˜ G.â‚ f âŠ—â‚ id          â‰ˆâŸ¨ unitorÊ³-commute-from âŸ©
        G.â‚ f âˆ˜ Ïâ‡’                âˆ

    âŠ²-ÎµË¡ : âŠ² âˆ˜ Îµ âŠ—â‚ G.â‚ f â‰ˆ G.â‚ f âˆ˜ Î»â‡’
    âŠ²-ÎµË¡ = begin
        âŠ² âˆ˜ Îµ âŠ—â‚ G.â‚ f            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ serializeâ‚â‚‚ âŸ©
        âŠ² âˆ˜ Îµ âŠ—â‚ id âˆ˜ id âŠ—â‚ G.â‚ f â‰ˆâŸ¨ pullË¡ (Equiv.sym (Î¼-identityË¡Â (M.â‚€ B))) âŸ©
        Î»â‡’ âˆ˜ id âŠ—â‚ G.â‚ f          â‰ˆâŸ¨ unitorË¡-commute-from âŸ©
        G.â‚ f âˆ˜ Î»â‡’                âˆ

  module _ {n : ğ’.Obj} where

    âŠ²-âŠ—-Î» : G.â‚ (+-Î».from {n}) âˆ˜ âŠ²-âŠ— âˆ˜ Îµ âŠ—â‚ id â‰ˆ Î»â‡’
    âŠ²-âŠ—-Î» = begin
        G.â‚ +-Î».from âˆ˜ (âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âˆ˜ Îµ âŠ—â‚ id â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ pullÊ³ mergeâ‚Ê³ âŸ©
        G.â‚ +-Î».from âˆ˜ âŠ² âˆ˜ (G.â‚ iâ‚ âˆ˜ Îµ) âŠ—â‚ G.â‚ iâ‚‚       â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âˆ˜âŸ¨ â‡’Îµ iâ‚ âŸ©âŠ—âŸ¨refl âŸ©
        G.â‚ +-Î».from âˆ˜ âŠ² âˆ˜ Îµ âŠ—â‚ G.â‚ iâ‚‚                  â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ âŠ²-ÎµË¡ iâ‚‚ âŸ©
        G.â‚ +-Î».from âˆ˜ G.â‚ iâ‚‚ âˆ˜ Î»â‡’                      â‰ˆâŸ¨ cancelË¡ ([ G ]-resp-âˆ˜ +-Î»âˆ˜iâ‚‚ â—‹ G.identity) âŸ©
        Î»â‡’                                              âˆ

    âŠ²-âŠ—-Ï : G.â‚ (+-Ï.from {n}) âˆ˜ âŠ²-âŠ— âˆ˜ id âŠ—â‚ Îµ â‰ˆ Ïâ‡’
    âŠ²-âŠ—-Ï = begin
        G.â‚ +-Ï.from âˆ˜ (âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ G.â‚ iâ‚‚) âˆ˜ id âŠ—â‚ Îµ â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ pullÊ³ mergeâ‚‚Ê³ âŸ©
        G.â‚ +-Ï.from âˆ˜ âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ (G.â‚ iâ‚‚ âˆ˜ Îµ)       â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âˆ˜âŸ¨ reflâŸ©âŠ—âŸ¨ â‡’Îµ iâ‚‚ âŸ©
        G.â‚ +-Ï.from âˆ˜ âŠ² âˆ˜ G.â‚ iâ‚ âŠ—â‚ Îµ                  â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ âŠ²-ÎµÊ³ iâ‚ âŸ©
        G.â‚ +-Ï.from âˆ˜ G.â‚ iâ‚ âˆ˜ Ïâ‡’                      â‰ˆâŸ¨ cancelË¡ ([ G ]-resp-âˆ˜ +-Ïâˆ˜iâ‚ â—‹ G.identity) âŸ©
        Ïâ‡’ Â                                             âˆ

F,âŠ—,Îµ : MonoidalFunctor ğ’-MC S
F,âŠ—,Îµ = record
    { F = G
    ; isMonoidal = record
        { Îµ = Îµ
        ; âŠ—-homo = âŠ²-âŠ—-homo
        ; associativity = âŠ²-âŠ—-Î±
        ; unitaryË¡ = âŠ²-âŠ—-Î»
        ; unitaryÊ³ = âŠ²-âŠ—-ÏÂ 
        }
    }

module F,âŠ—,Îµ = MonoidalFunctor F,âŠ—,Îµ
